plugins {
    id 'java'
    id 'jacoco'
}

group 'at.fhv.ec'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io'}
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.enabled true
        html.enabled true
//        xml.destination file("${buildDir}/jacocoXML")
//        html.destination file("${buildDir}/jacocoHtml")
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "Application.class"
            ])
        }))
    }
}

dependencies {
    implementation 'org.javamoney:moneta:1.4.2'
    implementation 'org.hibernate:hibernate-core:5.6.5.Final'
    implementation 'org.hibernate:hibernate-search-orm:5.11.10.Final'
    implementation 'org.postgresql:postgresql:42.3.3'
    implementation 'com.github.TeamF-SS22-SEM4:RMI-Shared-Lib:v2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'com.h2database:h2:2.1.210'
    implementation "org.mockito:mockito-core:3.+" //TODO change to testImpl when InstanceProvider doesn't mock anymore
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

build.dependsOn jacocoTestReport

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': 'at.fhv.ss22.ea.f.musicshop.backend.Application'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}